plugins {
    id 'groovy'
    id 'java'
    id 'maven'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.10.1"
    id "java-gradle-plugin"
    id "com.jfrog.bintray" version "1.8.4"
}

group 'com.kangxiaoguang.gradle.tools'
version '1.5'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    google()
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: uri('./repo')) //仓库的路径，此处是项目根目录下的 repo 的文件夹
        pom.artifactId = 'auto-app-version' //artifactId ，自行定义
    }
}

// Use java-gradle-plugin to generate plugin descriptors and specify plugin ids
gradlePlugin {
    plugins {
        autoAppVersionPlugin {
            id = 'com.kangxiaoguang.auto-app-version'
            implementationClass = 'com.kangxiaoguang.AutoAppVersionPlugin'
        }
    }
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
    website = 'http://www.kangxiaoguang.com/'
    vcsUrl = 'https://github.com/KevinLoveGitHub/AutoAppVersion.git'
    description = 'Auto App Version Plugin'
    tags = ['version', 'auto']

    plugins {
        autoAppVersionPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Gradle Auto App Version Plugin'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            url 'http://lovedev.org'

            packaging 'aar'
            //artifactId 'auto-app-version' //如果不设置默认使用settings.gradle中的配置 rootProject.name = 'auto-app-version'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/KevinLoveGitHub/AutoAppVersion.git'
                url 'https://github.com/KevinLoveGitHub/AutoAppVersion'
            }
            developers {
                developer {
                    name 'Kevin.Kang'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'auto-app-version'
        desc = 'auto app version plugin rename outfile name and android version name' //库文件描述
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/KevinLoveGitHub/AutoAppVersion.git'
        websiteUrl = 'https://github.com/KevinLoveGitHub/AutoAppVersion'
        issueTrackerUrl = 'https://github.com/KevinLoveGitHub/AutoAppVersion/issues'
        publicDownloadNumbers = true
        githubRepo = 'KevinLoveGitHub/AutoAppVersion' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file
        publish = true
        version {
            //name = '0.1' //如果不设置默认使用build.gradle中的version变量
            desc = 'Automatically modify version and file name' //版本描述
            released  = new Date()
            attributes = ['gradle-plugin': 'com.kangxiaoguang.gradle.tools:auto-app-version']
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    implementation "com.android.tools.build:gradle:2.3.3"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

